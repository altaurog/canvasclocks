window.drawClock = (canvas, hour, minute) ->
    ctx = canvas.getContext('2d')
    ctx.clearRect(0, 0, canvas.width, canvas.height)
    size = Math.min(canvas.width, canvas.height)
    center = size / 2
    r = 0.9 * center
    ctx.save()
    ctx.translate(center, center)
    ctx.save()
    ctx.beginPath()
    ctx.arc(0, 0, r, 0, 2 * Math.PI, true)
    for i in [1..12]
        ctx.rotate(2 * Math.PI / 12)
        ctx.moveTo(0, r)
        endr = if i % 3 then 0.95 else 0.9
        ctx.lineTo(0, r * endr)
    ctx.stroke()
    ctx.restore()

    ctx.save()
    ctx.rotate(minute * Math.PI / 30)
    ctx.beginPath()
    ctx.moveTo(r * 0.03, 0)
    ctx.lineTo(r * -0.03, 0)
    ctx.lineTo(0, r * -0.9)
    ctx.fillStyle = "black"
    ctx.fill()
    ctx.restore()

    hpos = (hour % 12) + minute / 60
    ctx.rotate(hpos * Math.PI / 6)
    ctx.beginPath()
    ctx.moveTo(r * 0.03, 0)
    ctx.lineTo(r * -0.03, 0)
    ctx.lineTo(0, r * -0.65)
    ctx.fillStyle = "#aaaaaa"
    ctx.fill()
    ctx.restore()

popRandom = (arr) ->
    i = Math.floor(Math.random() * arr.length)
    arr.splice(i, 1)[0]

inc = ->
    h = location.search or "?10"
    Number.parseInt(h.slice(1))


jQuery ->
    hours = [1..12]
    minutes = []
    while minutes.length < 12
        minutes = minutes.concat(m for m in [0..59] by inc())
    b = $("table")
    for i in [1..4]
        row = $("<tr/>")
        b.append(row)
        for j in [1..3]
            cell = $('<td><canvas width="200" height="200"></canvas></td>')
            row.append(cell)
            canvas = cell.children()[0]
            drawClock(canvas, popRandom(hours), popRandom(minutes))
